plugins {
    id 'idea'
    id 'java'
    id 'distribution'
    id 'com.install4j.gradle' version '9.0.6'
}

group = 'org.kodedevs'
version = rootProject.file('VERSION').text.trim()
description = 'Kode'

repositories {
    // https://mvnrepository.com/repos/central
    mavenCentral()

    // https://plugins.gradle.org/
    gradlePluginPortal()

    // https://www.ej-technologies.com/
    maven {
        url 'https://maven.ej-technologies.com/repository'
    }

    // 3rd-party libs
    flatDir { dirs 'libs' }
}

dependencies {
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'

    // Picocli
    implementation 'info.picocli:picocli:4.6.3'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.3'

    // Install4j Runtime
    compileOnly 'com.install4j:install4j-runtime:9.0.6'

    // JUnit5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.AZUL
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Aproject=${rootProject.group}/${rootProject.name}"]
    options.encoding = 'UTF-8'
}

jar {
    // Build a custom manifest
    manifest.attributes(['Manifest-Version'      : '1.0',
                         'Created-By'            : "Gradle API ${gradle.gradleVersion}",
                         'Built-By'              : System.getProperty('user.name'),
                         'Build-Jdk'             : "${System.getProperty('java.version')} (${System.getProperty('java.vendor')} ${System.getProperty('java.vm.version')})",
                         'Specification-Title'   : "Kode",
                         'Specification-Version' : project.version,
                         'Specification-Vendor'  : project.group,
                         'Implementation-Title'  : "Kode",
                         'Implementation-Version': project.version,
                         'Implementation-Vendor' : project.group,
                         'Build-Time'            : new Date().toString()])
    exclude 'META-INF/*.SF', 'META-INF/*.RSA', 'META-INF/*.DSA', 'META-INF/*.MF'
}

processResources {
    from(rootDir) {
        include 'LICENSE*', 'NOTICE*', 'VERSION*'
        into 'META-INF'
    }
}

distributions {
    main {
        contents {
            from jar
            from configurations.runtimeClasspath
        }
    }
}

install4j {
    if (System.env['INSTALL4J_HOME'] != null) installDir = file("$System.env.INSTALL4J_HOME")

    // Add License Key If Present
    license = System.env['INSTALL4J_LICENSE']
}

task buildInstallers(type: com.install4j.gradle.Install4jTask, group: BasePlugin.BUILD_GROUP) {
    dependsOn 'installDist'     // task that prepares the distribution for install4j

    projectFile = 'misc/installer/installer.install4j'
    release = project.version
    quiet = true    // Set it to false during debugging

    onlyIf {
        System.env['INSTALL4J_HOME'] != null
    }
}

wrapper {
    gradleVersion = '7.4.2'
    distributionType = Wrapper.DistributionType.BIN
}
